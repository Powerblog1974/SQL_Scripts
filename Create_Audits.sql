USE [master]
GO
CREATE SERVER AUDIT [Server-Security]
TO FILE 
(	FILEPATH = N'G:\AUDIT\'
	,MAXSIZE = 1024 MB
	,MAX_ROLLOVER_FILES = 2147483647
	,RESERVE_DISK_SPACE = OFF
)
WITH
(	QUEUE_DELAY = 1000
	,ON_FAILURE = CONTINUE
)
ALTER SERVER AUDIT [Server-Security] WITH (STATE = ON)
GO

CREATE SERVER AUDIT [Server-DDL]
TO FILE 
(	FILEPATH = N'G:\AUDIT\'
	,MAXSIZE = 1024 MB
	,MAX_ROLLOVER_FILES = 2147483647
	,RESERVE_DISK_SPACE = OFF
)
WITH
(	QUEUE_DELAY = 1000
	,ON_FAILURE = CONTINUE
)
ALTER SERVER AUDIT [Server-DDL] WITH (STATE = ON)
GO

CREATE SERVER AUDIT [Database-System]
TO FILE 
(	FILEPATH = N'G:\AUDIT\'
	,MAXSIZE = 1024 MB
	,MAX_ROLLOVER_FILES = 2147483647
	,RESERVE_DISK_SPACE = OFF
)
WITH
(	QUEUE_DELAY = 1000
	,ON_FAILURE = CONTINUE
)
ALTER SERVER AUDIT [Database-System] WITH (STATE = ON)
GO

CREATE SERVER AUDIT [Database-User-Change]
TO FILE 
(	FILEPATH = N'G:\AUDIT\'
	,MAXSIZE = 1024 MB
	,MAX_ROLLOVER_FILES = 2147483647
	,RESERVE_DISK_SPACE = OFF
)
WITH
(	QUEUE_DELAY = 1000
	,ON_FAILURE = CONTINUE
)
ALTER SERVER AUDIT [Database-User-Change] WITH (STATE = ON)
GO

CREATE SERVER AUDIT SPECIFICATION [ServerAuditSpecification-Security]
FOR SERVER AUDIT [Server-Security]
ADD (SERVER_ROLE_MEMBER_CHANGE_GROUP),
ADD (AUDIT_CHANGE_GROUP),
ADD (FAILED_DATABASE_AUTHENTICATION_GROUP),
ADD (DATABASE_LOGOUT_GROUP),
ADD (FAILED_LOGIN_GROUP),
ADD (SUCCESSFUL_LOGIN_GROUP),
ADD (DATABASE_CHANGE_GROUP),
ADD (DATABASE_OPERATION_GROUP),
ADD (SERVER_OPERATION_GROUP),
ADD (LOGIN_CHANGE_PASSWORD_GROUP),
ADD (SERVER_STATE_CHANGE_GROUP)
WITH (STATE = ON)
GO

CREATE SERVER AUDIT SPECIFICATION [ServerAuditSpecification-DDL]
FOR SERVER AUDIT [Server-DDL]
ADD (DATABASE_PERMISSION_CHANGE_GROUP),
ADD (SERVER_OBJECT_PERMISSION_CHANGE_GROUP),
ADD (DATABASE_CHANGE_GROUP),
ADD (DATABASE_OBJECT_CHANGE_GROUP),
ADD (SCHEMA_OBJECT_CHANGE_GROUP)
WITH (STATE = ON)
GO

USE MASTER;
CREATE DATABASE AUDIT SPECIFICATION [MASTER_DataAudit]
FOR SERVER AUDIT [Database-System]
ADD (DELETE ON DATABASE::[MASTER] BY [public])
,ADD (EXECUTE ON DATABASE::[MASTER] BY [public])
,ADD (INSERT ON DATABASE::[MASTER] BY [public])
,ADD (SELECT ON DATABASE::[MASTER] BY [public])
,ADD (UPDATE ON DATABASE::[MASTER] BY [public])
,ADD (AUDIT_CHANGE_GROUP)
,ADD (DATABASE_CHANGE_GROUP)
,ADD (DATABASE_OBJECT_CHANGE_GROUP)
,ADD (DATABASE_OBJECT_OWNERSHIP_CHANGE_GROUP)
,ADD (DATABASE_OBJECT_PERMISSION_CHANGE_GROUP)
,ADD (SCHEMA_OBJECT_CHANGE_GROUP)
,ADD (SCHEMA_OBJECT_OWNERSHIP_CHANGE_GROUP)
,ADD (SCHEMA_OBJECT_PERMISSION_CHANGE_GROUP)
,ADD (DATABASE_PRINCIPAL_CHANGE_GROUP)
,ADD (DATABASE_PERMISSION_CHANGE_GROUP)
,ADD (DATABASE_PRINCIPAL_IMPERSONATION_GROUP)
,ADD (DATABASE_ROLE_MEMBER_CHANGE_GROUP)
WITH (STATE = ON);

USE MSDB;
CREATE DATABASE AUDIT SPECIFICATION [MSDB_DataAudit]
FOR SERVER AUDIT [Database-System]
ADD (DELETE ON DATABASE::[MSDB] BY [public])
,ADD (EXECUTE ON DATABASE::[MSDB] BY [public])
,ADD (INSERT ON DATABASE::[MSDB] BY [public])
,ADD (SELECT ON DATABASE::[MSDB] BY [public])
,ADD (UPDATE ON DATABASE::[MSDB] BY [public])
,ADD (AUDIT_CHANGE_GROUP)
,ADD (DATABASE_CHANGE_GROUP)
,ADD (DATABASE_OBJECT_CHANGE_GROUP)
,ADD (DATABASE_OBJECT_OWNERSHIP_CHANGE_GROUP)
,ADD (DATABASE_OBJECT_PERMISSION_CHANGE_GROUP)
,ADD (SCHEMA_OBJECT_CHANGE_GROUP)
,ADD (SCHEMA_OBJECT_OWNERSHIP_CHANGE_GROUP)
,ADD (SCHEMA_OBJECT_PERMISSION_CHANGE_GROUP)
,ADD (DATABASE_PRINCIPAL_CHANGE_GROUP)
,ADD (DATABASE_PERMISSION_CHANGE_GROUP)
,ADD (DATABASE_PRINCIPAL_IMPERSONATION_GROUP)
,ADD (DATABASE_ROLE_MEMBER_CHANGE_GROUP)
WITH (STATE = ON);

/*
  Create Audit file pr. database for select statement on public
*/
declare @DB_ID as int,@DB_Name nvarchar(128),@strSQL_CMD nvarchar(max),@xmlData xml

declare cur_databases cursor
  for Select name,database_id from master.sys.databases where database_id > 4 and state = 0

Open cur_databases
fetch next from cur_databases into @DB_Name,@DB_ID

while @@FETCH_STATUS = 0
BEGIN
  Print @DB_Name
  set @strSQL_CMD = 'USE MASTER;
  CREATE SERVER AUDIT [Database-' + @DB_Name + '-Select]
TO FILE 
(	FILEPATH = N''G:\AUDIT\''
	,MAXSIZE = 1024 MB
	,MAX_ROLLOVER_FILES = 2147483647
	,RESERVE_DISK_SPACE = OFF
)
WITH
(	QUEUE_DELAY = 1000
	,ON_FAILURE = CONTINUE
);
ALTER SERVER AUDIT  [Database-' + @DB_Name + '-Select] WITH (STATE = ON);
'
   Print @strSQL_CMD
   EXECUTE sp_executesql   @strSQL_CMD

   set @strSQL_CMD = 'USE ['  +  @DB_Name + '];
   CREATE DATABASE AUDIT SPECIFICATION ['  +  @DB_Name + '_DataAudit-Select]
	FOR SERVER AUDIT [Database-' + @DB_Name + '-Select]
	ADD (SELECT ON DATABASE::['  +  @DB_Name + '] BY [public])
	,ADD (EXECUTE ON DATABASE::['  +  @DB_Name + '] BY [public])
	WITH (STATE = ON);'
  Print @strSQL_CMD
  EXECUTE sp_executesql   @strSQL_CMD

  set @strSQL_CMD = 'USE ['  +  @DB_Name + '];
	CREATE DATABASE AUDIT SPECIFICATION ['  +  @DB_Name + '_DataAudit-Change]
	FOR SERVER AUDIT [Database-User-Change]
	ADD (DELETE ON DATABASE::['  +  @DB_Name + '] BY [public])
	,ADD (INSERT ON DATABASE::['  +  @DB_Name + '] BY [public])
	,ADD (UPDATE ON DATABASE::['  +  @DB_Name + '] BY [public])
	WITH (STATE = ON);'
  Print @strSQL_CMD
  EXECUTE sp_executesql   @strSQL_CMD
  FETCH NEXT from cur_databases into @DB_Name,@DB_ID
END

close cur_databases
deallocate cur_databases
